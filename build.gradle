plugins {
    id 'java'
    id 'edu.sc.seis.launch4j' version '2.4.4'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.fox.Miota'
    }
}

ConfigObject conf = new ConfigSlurper().parse( file("${projectDir}/src/main/resources/version.properties").toURI().toURL());

repositories {
    mavenCentral()
}

group 'org.fox'
sourceCompatibility = 1.8
version conf.miota.version

dependencies {
    compile('org.jgrapht:jgrapht-core:1.3.0') {
        exclude group: 'org.jheaps', module: 'jheaps'
    }
    compile group: 'org.springframework', name: 'spring-web', version: '5.1.3.RELEASE'
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    compile group: 'org.springframework', name: 'spring-context', version: '5.1.3.RELEASE'
}

task uberJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'org.fox.Miota'
    }

    entryCompression = ZipEntryCompression.DEFLATED
    appendix = 'fat'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

launch4j {
    outfile = 'miota.exe'
    mainClassName = 'org.fox.Miota'
    copyConfigurable = uberJar.outputs.files
    icon = "${projectDir}/icon/mi.ico"
    productName = 'MiOTA'
    bundledJrePath = "jre"
    jreMinVersion = "1.8.0"
    jdkPreference = "preferJre"
    bundledJreAsFallback = "true"
    jar = "${buildDir}/libs/miota-fat-${project.version}.jar"
    headerType = "console"
    initialHeapSize = 16
    maxHeapSize = 512
}

task copyProperties(type: Copy) {
    from file("${projectDir}/src/main/resources/miota.properties")
    into file("${buildDir}/launch4j/")
}

createExe.dependsOn uberJar
createExe.dependsOn copyProperties